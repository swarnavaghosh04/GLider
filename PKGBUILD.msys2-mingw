# Maintainer: Swarnava Ghosh <swarnavaghosh04@gmail.com>

# For Windows MSYS2 MinGW (pacman)

# Manual installation:
# 1. make an empty directory
# 2. copy this file into that directory and remove the ".msys2-mingw" extension
# 3. run command: [MINGW_ARCH=(mingw32|mingw64)] makepkg-mingw -si
#		NOTE: you do not need to set MINGW_ARCH if you want to install both 64 and 32 bit versions
# 4. once installation is done, delete this directory.

_realname=GLider
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=0.1.0
pkgrel=1
pkgdesc="OpenGL Class Abstraction"
arch=('x86_64' 'i686')
url="https://github.com/swarnavaghosh04/GLider"
license=('MIT')
depends=()
makedepends=(
	"git"
	"${MINGW_PACKAGE_PREFIX}-make"
	"${MINGW_PACKAGE_PREFIX}-cmake"
	"${MINGW_PACKAGE_PREFIX}-ninja"
	"${MINGW_PACKAGE_PREFIX}-python-pip")
provides=("${pkgname}")
conflicts=("${pkgname}")
source=("git+${url}.git")
md5sums=('SKIP')

prepare() {
	cd "${srcdir}/${_realname}"
}

build() {
	cd "${srcdir}/${_realname}"
	mkdir -p "${srcdir}"/build-${MSYSTEM} && cd "${srcdir}"/build-${MSYSTEM}
	MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake.exe \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      -DCMAKE_BUILD_TYPE=Release \
      ../${_realname}
  	${MINGW_PREFIX}/bin/cmake.exe --build .
}

package() {
	cd "${srcdir}"/build-${MSYSTEM}
  	DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install .
}
